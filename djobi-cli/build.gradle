plugins {
    id 'application'
    id 'java'
    id 'idea'
    id "nebula.info-java" version "10.1.3"
}

java {
    mainClassName="io.datatok.djobi.Djobi"
}

configurations {
    djobiRelease

    djobiRelease.extendsFrom implementation
}

dependencies {

    /**
     * compileOnly here is for Idea, run with deps.
     */
    compileOnly project(':djobi-core')

    /**
     * Provided by Spark - but we dont want Spark dep here for cli
     */
    compileOnly sparkDependencyStrings.JACKSON_CORE
    compileOnly sparkDependencyStrings.APACHE_COMMONS_TEXT

    /**
     * Need core tests code
     */
    testImplementation(project(":djobi-tests"))

    testRuntimeOnly(
        project(path: ':djobi-core'),
        project(path: ':djobi-core', configuration: 'spark')
    )

    /**
     * To run Djobi via Idea.
     */
    runtimeOnly(
        project(path: ':djobi-elasticsearch', configuration: 'es7'),
        project(path: ':djobi-elasticsearch'),
        [group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.3.1'],
    )

    /**
     * Optional dep as it may not work on ARM64
     */
    compileOnly dependencyStrings.JANSI

    implementation dependencyStrings.PICOCLI
}

run {
    systemProperties System.getProperties()
}


djobi.createRelease()
